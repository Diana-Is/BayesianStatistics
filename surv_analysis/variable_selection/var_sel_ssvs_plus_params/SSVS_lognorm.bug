
#############################################################  
# SPIKE AND SLAB VARIABLE SELECTION: LOGNORM REGRESSION #####
#############################################################
model{
for(i in 1:N){
censored[i] ~ dinterval(t[i], cens[i]) 
t[i] ~ dlnorm(mu[i], tau)
mu[i] <- beta0 + inprod(X[i,], beta)
}
tau ~ dgamma(0.001,0.001)
sigma <- sqrt(1/tau)


# tracing the visited model
for (j in 1:p){
	TempIndicator[j]<- g[j] * pow(2, j) 
}
mdl<- 1 + sum(TempIndicator[]) # model index in binary coding 

beta0 ~ dnorm(0, 0.001)


c1 <- 1 / (pow(tau_ss, 2)) #reciprocal of the spike variance (precision)
c2 <- c1 / (pow(c_ss, 2)) # reciprocal of the slab variance 

##Since the Gaussian distribution
## is parametrized in terms of precision parameter

for(j in 1:p){
	bprior[j]<-0
	tprior[j] <-equals(g[j],0)*c1+equals(g[j],1)*c2 
	    # this is C1 if the first condition is met, 
	    #  c2 if the second is met
	beta[j] ~ dnorm(bprior[j],tprior[j]) 
	g[j] ~ dbern(0.5)
}
}
